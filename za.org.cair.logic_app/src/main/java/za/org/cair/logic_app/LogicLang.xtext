grammar za.org.cair.logic_app.LogicLang with org.eclipse.xtext.common.Terminals

generate logicLang "http://www.cair.org.za/logic_app/LogicLang"
// URL is just a namespace.

Model hidden(WS, COMMENT): // own-line comments
	// & used to mean any ordering
	propositions+=Proposition* &
	commands+=Command*
;


Proposition hidden(WS, COMMENT): // post-line comments
	'prop' sentence=Sentence
;

Sentence:
	Equivalence // will recurse down delegation chain
;

// How to deal with generic expressions:
// https://www.eclipse.org/Xtext/documentation/307_special_languages.html
// Summary: Avoid recursive definitions by specifying precedence
// by way of a "delegation chain" (top-down in this file)

// all rules here return Propositions, which ensures
// they produce AST's and not datatypes.
// Note where the precedence is the same, these rules are left-associative 


Equivalence returns Sentence:
	Implication ({Equivalence.left=current} ('<->' | 'iff') right=Implication)*
;

Implication returns Sentence:
	Disjunction ({Implication.left=current} ('->' | 'implies') right=Disjunction)*
;

Disjunction returns Sentence:
	Conjunction ({Disjunction.left=current} ('|' | 'or') right=Conjunction)*
;

Conjunction returns Sentence:
	Primary ({Conjunction.left=current} ('&' | 'and') right=Primary)*
;

Primary returns Sentence: 
// either terminal, negation, or restart the delegation chain
	Boolean |
	({Negation} ('~' | 'not') expression=Primary) |
	'(' Equivalence ')'
;

Boolean:
	BooleanLiteral |
	BooleanVariable
;

BooleanVariable:
	name=ID // user-declared variable name
;
	
BooleanLiteral:
	truth=BooleanValuesEnum
; 
	
// ignore the warnings here. This is valid syntax.
enum BooleanValuesEnum: 
	TRUE = 'T' | TRUE = 'True' |
	FALSE = 'F' | FALSE = 'False'
;


// Commands =====
Command:
	'cmd' (
		VariantTranslationCommand |
		JustParseCommand
		// TODO: other commands go here
	) 
;

enum LogicLangVariant:
	SYMBOL_OP = 'symbol variant' | 
	RESWORD_OP = 'reserved word variant'
;
VariantTranslationCommand:
	'translate' 'to' variant=LogicLangVariant
;

JustParseCommand:
	{JustParseCommand} 'just' 'parse'
;


terminal COMMENT: // #-initiated, ending on a newline char
	'#' !('\n'|'\r')* ('\r'? '\n')?
;
